PROGRAM Program_I2C_MQTT_Recv
	VAR
		mqtt_handle : DINT := -1;
		mqtt_ioImageRemote : ARRAY[0..1023] OF BYTE;
		mqtt_ioImageRemoteLen : DINT := 1;
		mqtt_state : MQTT_SUBSCRIBER_STATE := MQTT_SUBSCRIBER_STATE#INVALID;
		mqtt_subscribed : BOOL;
		mqtt_retval : MQTT_RC;

		i2c_handle : DINT := -1;
	END_VAR

	ENO := TRUE;

	mqtt_state := MQTT_GetState(ch := mqtt_handle, rc => mqtt_retval, ENO => ENO);
	/* make sure MQTT connection is established and messages are received */
	IF mqtt_state <> MQTT_SUBSCRIBER_STATE#CONNECTING AND mqtt_state <> MQTT_SUBSCRIBER_STATE#CONNECTED THEN
		mqtt_handle := MQTT_Connect(address := '10.1.111.208', clientId := 'RTS1', rc => mqtt_retval, ENO => ENO);
	END_IF;

	IF i2c_handle < 0 THEN
		i2c_handle := I2C_open(bus_id := 1);
	END_IF;

	IF ENO AND mqtt_state = MQTT_SUBSCRIBER_STATE#CONNECTED THEN
		IF NOT(mqtt_subscribed) THEN
			MQTT_Subscribe(ch := mqtt_handle, topic := MQTT_TOPICS#BATH, rc => mqtt_retval);
			mqtt_subscribed := mqtt_retval = MQTT_RC#OK;
		END_IF;

		IF mqtt_subscribed THEN
			REPEAT
				MQTT_Receive(ch := mqtt_handle,
								payload := REF(mqtt_ioImageRemote[0]),
								payload_len := TO_DINT(SIZEOF(mqtt_ioImageRemote)),
								len => mqtt_ioImageRemoteLen,
								rc => mqtt_retval);
				IF mqtt_retval <> MQTT_RC#OK THEN
					EXIT;
				END_IF;
				IF i2c_handle >= 0 THEN
					/* prepare data to be transfered over MQTT */
					I2C_PCF8574_write(fd := i2c_handle, address := 16#20, pin_values := CONVERT_BYTE_TO_ARR(mqtt_ioImageRemote[0]));
				END_IF;
			UNTIL TRUE
			END_REPEAT;
		END_IF;
	ELSE
		mqtt_subscribed := FALSE;
	END_IF;

END_PROGRAM
